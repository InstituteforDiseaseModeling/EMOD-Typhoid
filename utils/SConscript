# -*- mode: python; -*-
# This Python script, SConscript, invoked by the SConscript in the upper directory,
#
# 1. builds utils static library
import os
from subprocess import Popen, PIPE

Import("env")
if os.sys.platform != 'win32':
    git_rev_handle = Popen( "git rev-list HEAD --count".split(), stdout=PIPE )
    rev = git_rev_handle.stdout.read().rstrip() 
    if rev == "":
        rev = 0
        #rev = '"Unknown"'

    REV_STR = "-DREVISION_NUMBER='" + str(rev) + "'"
    env.Append( CCFLAGS=[ REV_STR ] )

    git_branch_handle = Popen( "git rev-parse --abbrev-ref HEAD".split(), stdout=PIPE )
    branch = git_branch_handle.stdout.read().rstrip()
    if branch == "":
        branch = "unknownbranch"
    git_sha_handle = Popen( "git log --pretty=format:'%h' -n 1".split(), stdout=PIPE )
    sha1 = git_sha_handle.stdout.read().rstrip().strip("'")
    if sha1 == "":
        sha1 = "unknownsha1"
    BR_STR = '-DSCCS_BRANCH=\'"' + branch + '(' + sha1 + ')"\''
    env.Append( CCFLAGS=[ BR_STR ] )

    git_date_handle = Popen( "git log -1 --pretty=%ai".split(), stdout=PIPE )
    git_date = git_date_handle.stdout.read().rstrip().replace( ' ', '_' )
    if git_date == "":
        git_date = "unknown_date_time"
    GIT_DATE_STR = '-DSCCS_DATE=\'"' + git_date + '"\''
    env.Append( CCFLAGS=[ GIT_DATE_STR ] )
    USER_STR = '-DBUILDER_NAME=\'"{0}"\''.format(os.environ['USER'])
    env.Append( CCFLAGS=[ USER_STR ] )
else:
    root = Dir('#').abspath
    version_info = '{0}\\scripts\\gitversion.cmd {0}\\utils'.format(root)
    print("Executing '{0}'".format(version_info))
    os.system(version_info)

utilsSrcFiles = [
    "BinaryArchiveReader.cpp",
    "BinaryArchiveWriter.cpp",
    "Configuration.cpp",
    "Configure.cpp",
    "Coredump.cpp",
    "DurationDistribution.cpp",
    "Environment.cpp",
    "Exceptions.cpp",
    "FileSystem.cpp",
    "IArchive.cpp",
    "IdmDateTime.cpp",
    "IdmMpi.cpp",
    "InterpolatedValueMap.cpp",
    "ISerializable.cpp",
    "ISupports.cpp",
    "JsonFullReader.cpp",
    "JsonFullWriter.cpp",
    "JsonObject.cpp",
    "JsonRawReader.cpp",
    "JsonRawWriter.cpp",
    "Log.cpp",
    "MathFunctions.cpp",
    "../rapidjson/modp/modp_numtoa.cpp",
    "ProgramOptions.cpp",
    "ProgVersion.cpp",
    "RANDOM.cpp",
    "RapidJsonImpl.cpp",
    "Serializer.cpp",
    "Sigmoid.cpp",
    "StatusReporter.cpp",
    "stdafx.cpp",
    "suids.cpp",
    "Types.cpp",
    "ValidationLog.cpp"
]

utilsIncFiles = [
    'BoostLibWrapper.h',
    'Configuration.h',
    'ConfigurationImpl.h',
    'Configure.h',
    'EnumSupport.h',
    'Environment.h',
    'FactorySupport.h',
    "InterpolatedValueMap.h",
    'ISupports.h',
    'Log.h',
    'MathFunctions.h',
    'Profile.h',
    'ProgVersion.h',
    'SimpleTypemapRegistration.h',
    "StatusReporter.h",
    'Sugar.h',
    'ValidationLog.h',
    'version_info.h'
]

utilsLocalInc = ['stdafx.h']

utilsLib = env.StaticLibrary('utils', utilsSrcFiles)

msvc = GetOption( "MSVC" )
if msvc is not None and msvc != False and msvc != "":

    bvar = env['BUILD_VARIANT'] + '|x64'
    print "Utils: Generating MSVS" + env['MSVS_VERSION'] + " project files for '%s'" % bvar
    
    env.MSVSProject(target = 'UtilsScons' + env['MSVSPROJECTSUFFIX'],
		srcs = utilsSrcFiles,
		incs = utilsIncFiles,
		localincs = utilsLocalInc,
		buildtarget = utilsLib,
                auto_build_solution = 0,
                variant = bvar)
